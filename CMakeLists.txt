cmake_minimum_required(VERSION 3.1)
project(OGDF-PROJECT CXX)
include_directories(include)

# groups source files according to the directory structure
function(group_files SOURCES)
  foreach(SOURCE_FILE ${${SOURCES}})
    get_filename_component(GROUP "${SOURCE_FILE}" PATH)
    string(REPLACE "${CMAKE_SOURCE_DIR}" "" GROUP "${GROUP}")
    string(REPLACE "/" "\\" GROUP "${GROUP}")

    set(GROUP "${GROUP}\\")
    foreach(REPL ${ARGN})
      string(REPLACE "\\${REPL}\\" "\\" GROUP "${GROUP}")
    endforeach()

    source_group("${GROUP}" FILES "${SOURCE_FILE}")
  endforeach()
endfunction()

# build type configuration
set(BUILD_TYPES Debug Release)
if(CMAKE_CONFIGURATION_TYPES)
  set(CMAKE_CONFIGURATION_TYPES ${BUILD_TYPES} CACHE STRING "" FORCE)
else()
  if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "Release" CACHE STRING "Type of build to be created." FORCE)
  endif()
  set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS ${BUILD_TYPES})
endif()

# cache configuration
option(BUILD_SHARED_LIBS "Whether to build shared libraries instead of static ones." OFF)
set(OGDF_MEMORY_MANAGER "POOL_TS" CACHE STRING "Memory manager to be used.")
option(OGDF_DEBUG "Whether to include OGDF assertions in Debug mode (increased runtime)." ON)
option(OGDF_SEPARATE_TESTS "Whether to build separate test executables (used for continuous integration)" OFF)
set_property(CACHE OGDF_MEMORY_MANAGER PROPERTY STRINGS POOL_TS POOL_NTS MALLOC_TS)
set(COIN_SOLVER "CLP" CACHE STRING "Linear program solver to be used by COIN.")
set_property(CACHE COIN_SOLVER PROPERTY STRINGS CLP CPX GRB)
set(COIN_EXTERNAL_SOLVER_INCLUDE_DIRECTORIES "" CACHE PATH "Locations of required header files for the external LP solver.")
set(COIN_EXTERNAL_SOLVER_LIBRARIES "" CACHE FILEPATH "Libraries for the external LP solver.")
set(CMAKE_CXX_STANDARD 11)

# COIN library
file(GLOB_RECURSE COIN_SOURCES src/coin/*.cpp include/coin/*.h)
if(NOT COIN_SOLVER STREQUAL "GRB")
  list(REMOVE_ITEM COIN_SOURCES "${CMAKE_SOURCE_DIR}/src/coin/Osi/OsiGrbSolverInterface.cpp")
endif()
if(NOT COIN_SOLVER STREQUAL "CPX")
  list(REMOVE_ITEM COIN_SOURCES "${CMAKE_SOURCE_DIR}/src/coin/Osi/OsiCpxSolverInterface.cpp")
endif()
if (BUILD_SHARED_LIBS)
  add_library(COIN SHARED ${COIN_SOURCES})
else (BUILD_SHARED_LIBS)
    add_library(COIN ${COIN_SOURCES})
endif (BUILD_SHARED_LIBS)
group_files(COIN_SOURCES "coin")
target_include_directories(COIN PUBLIC include/coin)
target_compile_definitions(COIN PUBLIC -DCLP_BUILD -DCOINUTILS_BUILD -DOSI_BUILD -DSYMPHONY_BUILD -D__OSI_CLP__ -DCOMPILE_IN_CG -DCOMPILE_IN_CP -DCOMPILE_IN_LP -DCOMPILE_IN_TM -DUSE_CGL_CUTS -DHAVE_CONFIG_H)
install(TARGETS COIN ARCHIVE DESTINATION ${CMAKE_INSTALL_PREFIX}/lib LIBRARY DESTINATION ${CMAKE_INSTALL_PREFIX}/lib COMPONENT library)

# external LP solver
if(COIN_EXTERNAL_SOLVER_LIBRARIES)
  target_link_libraries(COIN PUBLIC ${COIN_EXTERNAL_SOLVER_LIBRARIES})
  foreach(EXT_LIB ${COIN_EXTERNAL_SOLVER_LIBRARIES})
    if(NOT EXISTS ${EXT_LIB} OR IS_DIRECTORY ${EXT_LIB})
      message(SEND_ERROR "The provided library does not exist: ${EXT_LIB}")
    endif()
  endforeach()
endif()
if(COIN_EXTERNAL_SOLVER_INCLUDE_DIRECTORIES)
  foreach(EXT_DIR ${COIN_EXTERNAL_SOLVER_INCLUDE_DIRECTORIES})
    if(NOT IS_DIRECTORY ${EXT_DIR})
      message(SEND_ERROR "The provided directory is invalid: ${EXT_DIR}")
    endif()
  endforeach()
  target_include_directories(COIN PUBLIC ${COIN_EXTERNAL_SOLVER_INCLUDE_DIRECTORIES})
endif()

# OGDF library
file(GLOB_RECURSE OGDF_SOURCES src/ogdf/*.cpp include/ogdf/*.h)
if(NOT OGDF_COMPILE_LEGACY)
  file(GLOB_RECURSE OGDF_LEGACY_SOURCES src/ogdf/legacy/*.cpp)
  foreach(legacyFile ${OGDF_LEGACY_SOURCES})
    list(REMOVE_ITEM OGDF_SOURCES ${legacyFile})
  endforeach()
endif()
if (BUILD_SHARED_LIBS)
    add_library(OGDF SHARED ${OGDF_SOURCES})
else (BUILD_SHARED_LIBS)
    add_library(OGDF ${OGDF_SOURCES})
endif (BUILD_SHARED_LIBS)
group_files(OGDF_SOURCES "ogdf")
target_compile_features(OGDF PUBLIC cxx_range_for)
if(COIN_EXTERNAL_SOLVER_INCLUDE_DIRECTORIES)
  target_include_directories(OGDF PUBLIC ${COIN_EXTERNAL_SOLVER_INCLUDE_DIRECTORIES})
endif()
install(DIRECTORY include/ DESTINATION ${CMAKE_INSTALL_PREFIX}/include FILES_MATCHING PATTERN "*.h")
install(DIRECTORY include/ DESTINATION ${CMAKE_INSTALL_PREFIX}/include FILES_MATCHING PATTERN "*.inc")
install(TARGETS OGDF ARCHIVE DESTINATION ${CMAKE_INSTALL_PREFIX}/lib LIBRARY DESTINATION ${CMAKE_INSTALL_PREFIX}/lib COMPONENT library)

# tests
option(OGDF_ENABLE_TESTS "OGDF_ENABLE_TESTS" OFF)
if (OGDF_ENABLE_TESTS)
  file(GLOB_RECURSE TEST_SOURCES test/src/*.cpp)
  group_files(TEST_SOURCES "test")
  if(OGDF_SEPARATE_TESTS)
    SET(EXECUTABLE_OUTPUT_PATH "${PROJECT_BINARY_DIR}/test/bin")
    foreach(SOURCE_FILE ${TEST_SOURCES})
      get_filename_component(TARGET ${SOURCE_FILE} NAME_WE)
      if(NOT ${TARGET} STREQUAL "main")
        add_executable(${TARGET} ${SOURCE_FILE} "test/src/main.cpp")
        target_include_directories(${TARGET} PUBLIC test/include)
        target_link_libraries(${TARGET} OGDF COIN)
        if(CMAKE_COMPILER_IS_GNUCXX OR "${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
  	target_link_libraries(${TARGET} pthread)
        endif()
      endif()
    endforeach()
  else()
    add_executable(test-ogdf ${TEST_SOURCES})
    target_compile_features(test-ogdf PUBLIC cxx_range_for)
    target_include_directories(test-ogdf PUBLIC test/include)
    target_link_libraries(test-ogdf OGDF COIN)
    if(CMAKE_COMPILER_IS_GNUCXX OR "${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
      target_link_libraries(test-ogdf pthread)
    endif()
  endif()
endif(OGDF_ENABLE_TESTS)

# various compile flags
if(MSVC)
  add_definitions(/bigobj)
  set(CMAKE_CXX_STACK_SIZE "10000000")
endif()

# create autogen header
option(COIN_SOLVER_IS_EXTERNAL "Enable External Coin Solver" OFF)
if(COIN_SOLVER STREQUAL "CLP")
  set(COIN_SOLVER_IS_EXTERNAL OFF)
else()
  set(COIN_SOLVER_IS_EXTERNAL ON)
endif()
configure_file("${CMAKE_SOURCE_DIR}/config_autogen.h.in" "${CMAKE_SOURCE_DIR}/include/ogdf/internal/config_autogen.h" @ONLY)

# doxygen
find_package(Doxygen)
set(DOC_DIR "${CMAKE_SOURCE_DIR}/doc")
add_custom_target(doc ${DOXYGEN_EXECUTABLE} ${DOC_DIR}/ogdf-doxygen.cfg WORKING_DIRECTORY ${DOC_DIR})
